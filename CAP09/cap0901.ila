// programa PESQUISA_BINÁRIA (cap0901.ila)

variaveis
  matriz numerico VLR[10]
  numerico I, J, COMECO, FINAL, MEIO, PESQ, X
  caracter RESP
  logico ACHA
inicio

  para I = 1 ate 10 passo 1 
    ler VLR[I]
  proximo

  // {*** início trecho de ordenação ***}

  para I = 1 ate 9 passo 1
    para J = I + 1 ate 10 passo 1
      se (VLR[I] > VLR[J]) entao
        X = VLR[I]
        VLR[I] = VLR[J]
        VLR[J] = X
      fim_se
    proximo
  proximo

  // {*** fim trecho de ordenação ***}

  // {*** início trecho de pesquisa binária ***}

  RESP = "S"
  faca enquanto (RESP = "S") ou (RESP = "s")
    escrever "Entre o nome a ser pesquisado:"
    ler PESQ
    COMECO = 1
    FINAL = 10
    ACHA = falso
    faca enquanto (COMECO <= FINAL) e (ACHA = falso)
      MEIO = inteiro((COMECO + FINAL) / 2)
      se (PESQ = VLR[MEIO]) entao
        ACHA = verdadeiro
      senao
        se (PESQ < VLR[MEIO]) entao
          FINAL = MEIO - 1
        senao
          COMECO = MEIO + 1
        fim_se
      fim_se
    fim_enquanto
    se (ACHA = verdadeiro) entao
      escrever PESQ, " foi localizado na posicao ", MEIO
    senao
      escrever PESQ, " não foi localizado"
    fim_se
    escrever "Deseja continuar? (S)/(N)AO:"
    ler RESP
  fim_enquanto

  // {*** fim trecho de pesquisa binária ***}

fim
